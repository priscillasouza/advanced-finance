plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
}

def apiKeyPropertiesFile = rootProject.file("apikey.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

android {
    namespace 'com.advancedfinance.entrance'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            buildConfigField "String", "GOOGLE_API_OAUTH", apiKeyProperties["GOOGLE_API_OAUTH"]
        }
        debug {
            buildConfigField "String", "GOOGLE_API_OAUTH", apiKeyProperties["GOOGLE_API_OAUTH_DEBUG"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }

    lint {
        quiet false
        abortOnError true
        checkDependencies true
    }
    resourcePrefix "${project.name.replace("-", "_")}_"
}

dependencies {

    implementation(project(":core"))
    implementation(project(":account_finance"))

    testImplementation libs.test.junit
    androidTestImplementation libs.bundles.android.test
    androidTestImplementation libs.androidx.room.testing

    implementation libs.androidx.corektx
    implementation libs.bundles.navigation
    implementation libs.androidx.appcompat
    implementation libs.android.material
    implementation libs.androidx.constraintlayout
    implementation libs.bundles.coroutines
    implementation libs.bundles.lifecycle
    implementation libs.koin.android
    implementation libs.bundles.networking
    implementation libs.bundles.room
    implementation libs.google.play.services.auth
    implementation libs.androidx.biometric
    implementation libs.androidx.viewpager2
    implementation libs.relex.circleindicator
}

configurations.implementation {
    exclude group: 'com.intellij', module: 'annotations'
}